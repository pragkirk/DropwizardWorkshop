/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'pragkirk' at '9/12/14 6:33 AM' with Gradle 2.1
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/2.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.7'

mainClassName = 'com.kirkk.loancalc.LoanCalculatorApp'
version = '0.1.0'

repositories {
    mavenCentral()
}

configurations {
    capsule
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    capsule 'co.paralleluniverse:capsule:0.4.0'
    testCompile 'junit:junit:4.11'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "loancalc.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies
    from { sourceSets.main.resources }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    manifest {
        attributes(
            'Main-Class'  :   'Capsule',
            'Application-Class'   : mainClassName,
            'Application-Version' : version,
            'Min-Java-Version' : '1.7.0',
            'JVM-Args' : run.jvmArgs.join(' '),
            'System-Properties' : (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/loancalc.yml']).collect { k,v -> "$k=$v" }.join(' '),
        )
    }
}

run {
    systemProperty "dropwizard.config", "build/resources/main/loancalc.yml"
}
